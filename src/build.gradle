// https://github.com/domaframework/simple-boilerplate/blob/master/build.gradle
plugins {
    id 'java-library'
    id 'application'
    id 'com.diffplug.eclipse.apt' version '3.24.0'
    id 'com.github.spotbugs' version '4.7.1'
}

if (hasProperty('mavenLocal')) {
    repositories {
        mavenLocal()
    }
} else {
    repositories {
        maven {
            url 'https://maven.pkg.github.com/project-tsurugi/*'
            content {
                includeGroupByRegex 'com\\.tsurugidb.*'
            }
            credentials {
                username findProperty('gpr.user') ?: System.getenv('GPR_USER')
                password findProperty('gpr.key') ?: System.getenv('GPR_KEY')
            }
        }
    }
}

repositories {
    mavenCentral {
        content {
            excludeGroupByRegex 'com\\.tsurugidb.*'
        }
    }
}


java {
    mainClassName = 'com.tsurugidb.benchmark.phonebill.app.Main'
}
distTar {
    archiveName = "phone-bill.tar"
}
distZip {
    archiveName = "phone-bill.zip"
}
jar {
    archiveName = "phone-bill.jar"
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 8, 'hours'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}



test {
    useJUnitPlatform() {
        excludeTags 'oracle'
        excludeTags 'copy-command'
        excludeTags 'tsurugi'
    }
    testLogging {
        showStandardStreams true
        events 'started', 'skipped', 'passed', 'failed'
        exceptionFormat 'full'
    }
    if (project.hasProperty("phone-bill.url")) {
        systemProperty "phone-bill.url", project.getProperty("phone-bill.url")
    }
}


dependencies {
  api 'com.tsurugidb.iceaxe:iceaxe-core:0.0.1-SNAPSHOT'

  // https://mvnrepository.com/artifact/org.postgresql/postgresql
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.16'
  // https://mvnrepository.com/artifact/com.oracle.ojdbc/ojdbc8
  runtimeOnly group: 'com.oracle.database.jdbc', name: 'ojdbc8', version: '21.1.0.0'
  // https://mvnrepository.com/artifact/com.oracle.ojdbc/orai18n
  runtimeOnly group: 'com.oracle.database.nls', name: 'orai18n', version: '21.1.0.0'
  implementation group: 'com.oracle.database.jdbc', name: 'ucp', version: '21.1.0.0'
  testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.2")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.2")
  testRuntimeOnly("org.junit.platform:junit-platform-launcher:1.0.0") // for IDE
  implementation "commons-cli:commons-cli:1.4"
  implementation "ch.qos.logback:logback-classic:1.2.3"
  implementation "org.apache.commons:commons-math3:3.6.1"
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
  compileOnly"com.github.spotbugs:spotbugs-annotations:4.2.3"
  testCompileOnly"com.github.spotbugs:spotbugs-annotations:4.2.3"
}

startScripts {
    applicationName = 'run'
}

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
}


spotbugsMain {
    ignoreFailures = true
    reports {
        html {
            enabled = !project.hasProperty("ci")
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
        xml {
            enabled = project.hasProperty("ci")
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.xml")
        }
    }
}

eclipse {
    classpath {
        file {
            whenMerged { classpath ->
                classpath.entries.removeAll { it.path == '.apt_generated' }
            }
            withXml { provider ->
                def node = provider.asNode()
                // specify output path for .apt_generated
                node.appendNode( 'classpathentry', [ kind: 'src', output: 'bin/main', path: '.apt_generated'])
            }
        }
    }
}

task showTsubakuroManifest {
    doLast {
        def tsubakuroJar = configurations["compileClasspath"].filter {
            it.name.startsWith("tsubakuro-session")
        }
        def version = resources.text.fromArchiveEntry(tsubakuroJar, "META-INF/MANIFEST.MF")
        print(version.asString())
    }
}

task showIceaxeManifest {
    doLast {
        def iceaxeJar = configurations["compileClasspath"].filter {
            it.name.startsWith("iceaxe-core")
        }
        def version = resources.text.fromArchiveEntry(iceaxeJar, "META-INF/MANIFEST.MF")
        print(version.asString())
    }
}
